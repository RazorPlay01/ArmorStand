load("@armorstand//rule:extract_jar.bzl", "extract_jar")
load("@armorstand//rule:merge_mapping.bzl", "merge_mapping", "merge_mapping_input")
load("@armorstand//rule:remap_jar.bzl", "remap_jar")
load("@armorstand//rule:merge_jar.bzl", "merge_jar")
load("@armorstand//:properties.bzl", "game_version")
load("@bazel_skylib//rules:expand_template.bzl", "expand_template")
load("@rules_java//java:defs.bzl", "java_library")

extract_jar(
    name = "intermediary_mapping",
    entry_path = "mappings/mappings.tiny",
    filename = "_mappings/intermediary.tiny",
    input = "@maven//:net_fabricmc_intermediary_v2",
)

extract_jar(
    name = "yarn_mapping",
    entry_path = "mappings/mappings.tiny",
    filename = "_mappings/yarn.tiny",
    input = "@maven//:net_fabricmc_yarn_v2",
)

merge_mapping_input(
    name = "yarn_input",
    file = ":yarn_mapping",
    format = "tinyv2",
)

merge_mapping_input(
    name = "intermediary_input",
    file = ":intermediary_mapping",
    format = "tinyv2",
    source_namespace = "intermediary",
)

merge_mapping(
    name = "merged_mapping",
    complete_namespace = {
        "named": "intermediary",
        "intermediary": "official",
    },
    inputs = [
        ":yarn_input",
        ":intermediary_input",
    ],
    output = "merged.tiny",
    output_source_namespace = "official",
)

remap_jar(
    name = "remapped_client_intermediary",
    inputs = ["@minecraft//:%s_client" % game_version],
    mapping = ":merged_mapping",
    from_namespace = "official",
    to_namespace = "intermediary",
)

remap_jar(
    name = "remapped_client_named",
    inputs = ["@minecraft//:%s_client" % game_version],
    mapping = ":merged_mapping",
    from_namespace = "official",
    to_namespace = "named",
)

remap_jar(
    name = "remapped_deps",
    classpath = [
        ":remapped_client_intermediary",
    ],
    inputs = [
        "@maven//:net_fabricmc_fabric_api_fabric_api",
        "@maven//:net_fabricmc_fabric_loader",
        "@maven//:net_fabricmc_sponge_mixin",
        "@maven//:io_github_llamalad7_mixinextras_fabric",
        "@maven//:eu_pb4_placeholder_api",
    ],
    mixin = True,
    remap_transitive_deps = True,
    remove_jar_in_jar = True,
    mapping = ":merged_mapping",
    from_namespace = "intermediary",
    to_namespace = "named",
)

expand_template(
    name = "fabric_mod_json",
    template = "fabric.mod.json",
    substitutions = {
        "$version": "14.0.0-rc.2+armorstand",
    },
    out = "_generated/fabric.mod.json",
)

java_library(
    name = "fabric_mod_json_stripped",
    resources = [":fabric_mod_json"],
    resource_strip_prefix = "_generated",
)

java_library(
    name = "modmenu_unmapped",
    srcs = glob(["com/terraformersmc/modmenu/**/*.java"]),
    deps = [
        ":remapped_deps",
        ":remapped_client_named",
        "@maven//:org_quiltmc_quilt_loader",
        "@maven//:org_jetbrains_annotations",
        "@minecraft//:%s_client_libraries" % game_version,
    ],
    resources = glob([
        "assets/**/*",
        "high_contrast/**/*",
        "programmer_art/**/*",
    ]) + [
        "mixins.modmenu.json",
    ],
)

remap_jar(
    name = "modmenu_without_fabric_mod_json",
    inputs = [":modmenu_unmapped"],
    classpath = [":remapped_client_named"],
    mapping = ":yarn_mapping",
    mixin = True,
    from_namespace = "named",
    to_namespace = "intermediary",
)

merge_jar(
    name = "modmenu",
    visibility = ["//visibility:public"],
    deps = [
        ":modmenu_without_fabric_mod_json",
        ":fabric_mod_json_stripped",
    ],
)